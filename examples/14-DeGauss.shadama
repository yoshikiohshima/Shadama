program "DeGauss"
breed Pict (pos:vec2, color:vec4, origPos:vec2, origC:vec4)
breed Filler (pos:vec2, color:vec4)
breed Button (pos:vec2, color:vec4)
breed Right (pos:vec2, color:vec4)
patch Blur (level:float)

data Presentation("presentation.png", "image")
data BlurBlue("blur-blue.png", "image")
data Degauss("button.png", "image")
data RightButton("rightbutton.png", "image")
data Noise("degauss.mp3", "audio")

event begin
event showButton
event showRight

  
def move(x:float, y:float) {
  this.pos = this.pos:vec2 + vec2(x, y)
}

def remember1() {
  this.origPos = this.pos:vec2
  this.origC = this.color:vec4
}

def storeBlur(blur:object) {
  blur.level = this.color:vec4.b
}

def restore() {
  this.pos = this.origPos:vec2
  this.color = this.origC:vec4
}

def colorize2(blur:object) {
   var p = 1.0 - blur.level:float;
   if (p < 0.9) {
     this.color = vec4(this.origC:vec4.r * p + this.origC.g * (1 - p),
     		       this.origC.g * p,
		       this.origC.b * (1 - p),
                       this.origC.a);
   }
}

def degauss(step:float, begin:float) {
  var x:float = step * 50.0;
  var timeOff:float = step - begin;
  var off = this.origPos:vec2 - vec2(128, 128);
  if (step - begin > 0.4) {
    var arg = -2.4 - timeOff;


    x = x + dot(off / 100, vec2(sin(step * 3), 1))
    var c:float = cos(x) * exp(arg);
    var s:float = sin(x) * exp(arg);
  
    this.pos = this.origPos:vec2 + (this.origPos:vec2 - vec2(128, 128)) * vec2(c, s)
  }
}

def degaussColor(step:float, begin:float) {
  this.color = vec4(this.color:vec4.rgb - (this.color.rgb - this.origC:vec4.rgb) * 0.01, 1);
}
  
static setup() {
  Pict.fillImage("pos", "color", Presentation)
  Pict.remember1()
  Filler.fillImage("pos", "color", BlurBlue)
  Filler.storeBlur(Blur)
  Button.fillImage("pos", "color", Degauss)
  Button.move(760, 160)

  Right.fillImage("pos", "color", RightButton)
  Right.move(960, 0)

  Pict.colorize2(Blur)
  Pict.draw()
  begin = -1
  showButton = 0
  showRight = 0
  loop.start()
}

static loop() {
  Display.clear();
  if (mousedown.x > 950) {
    if (showButton == 0) {
      showButton = 1;
    }
    if (mousemove.y < 200 && mousemove.y > 60) {
      if (begin == -1) {
        begin = time;
        Display.playSound(Noise);
      }
    }
  }
  if (mousemove.x > 960 && mousemove.y < 50) {
    showRight = 1;
  }
  if (begin != -1) {
    Pict.degauss(time, begin);
    Pict.degaussColor(time, begin);
  }
  Pict.draw();
  if (showButton == 1) {
     Button.draw();
  }
  if (showRight == 1) {
     Right.draw();
  }
  if (mousedown.x > 960 && mousedown.y < 50) {
    Display.loadProgram("17-Goals.shadama");
  }
}
