program "Bounce"

breed Turtle (x, y, dx, dy, r, g, b, a)
breed Filler (x, y)
patch Field (nx, ny, r, g, b, a)

def setColor() {
  this.r = this.x / 512.0;
  this.g = this.y / 512.0;
  this.b = 0.0;
  this.a = 1.0;
}

def clear(field) {
  field.r = 0.0;
  field.g = 0.0;
  field.b = 0.0;
  field.a = 0.0;
  field.nx = 0.0;
  field.ny = 0.0;
}

def fillCircle(cx, cy, r, field) {
  var dx = this.x - cx;
  var dy = this.y - cy;
  var dr = sqrt(dx * dx + dy * dy);
  if (dr < r) {
    field.r = 0.2;
    field.g = 0.2;
    field.b = 0.8;
    field.a = 1.0;
    field.nx = dx / r;
    field.ny = dy / r;
  }
}

def fillRectangle(x1, y1, x2, y2, field) {
  var x = this.x;
  var y = this.y;
  var cx = (x1 + x2) / 2;
  var cy = (y1 + y2) / 2;
  var s = (y2 - y1) / (x2 - x1);
  if (x1 <= x && x < x2 && y1 <= y && y < y2) {
    var myY = y - cy;
    var myX = x - cx;
    var myS = myY / myX;
    field.r = 0.2;
    field.g = 0.2;
    field.b = 0.8;
    field.a = 1.0;
    if (abs(myS) > s) {
      field.nx = 0;
      field.ny = sign(myY);
     } else {
      field.nx = sign(myX);
      field.ny = 0;
     }        
  }
}  

def zeroDir() {
  this.dx = 0.0;
  this.dy = 0.0;
}
 
def bounce(field) {
  var nx = field.nx;
  var ny = field.ny;
  var dx = this.dx;
  var dy = this.dy - 0.01;
  var dot = dx * nx + dy * ny;
  var rx = dx;
  var ry = dy;
  var origV = sqrt(dx * dx + dy * dy);

  if (dot < 0.0) {
    rx = dx - 2.0 * dot * nx;
    ry = dy - 2.0 * dot * ny;
    var norm = sqrt(rx * rx + ry * ry);
    rx = rx / (norm / origV);
    ry = ry / (norm / origV);
  }

  var newX = this.x + dx;
  var newY = this.y + dy;

  if (newX < 0.0) {
    newX = -newX;
    rx = -rx * 0.9;
  }
  if (newX > u_resolution.x) {
    newX = u_resolution.x - (newX - u_resolution.x);
    rx = -rx * 0.9;
  }
  if (newY < 0.0) {
    newY = mod(newY, u_resolution.y);
    ry = -0.1;
  }
  if (newY > u_resolution.y) {
    newY = u_resolution.y - (newY - u_resolution.y);
    ry = -ry;
  }

  this.x = newX;
  this.y = newY;
  this.dx = rx;
  this.dy = ry;
}

static loop() {
  Filler.clear(Field);
  Filler.fillRectangle(60, 300, 120, 360, Field);
  Filler.fillRectangle(310, 140, 350, 180, Field);
  Filler.fillCircle(300, 95, 25, Field);
  Turtle.bounce(Field);
  Display.clear();
  Field.draw();
  Turtle.draw();
}

static setup() {
  Filler.fillSpace("x", "y", 512, 512);
  Turtle.setCount(3000);
  Turtle.fillRandom("x", 0, 512);
  Turtle.fillRandom("y", 256, 512);
  Turtle.fillRandomDir("dx", "dy");
  Turtle.setColor();
  loop.start();
}

